"Vorple Unit Tests"

Include Vorple by Juhana Leinonen.
Include Vorple Command Prompt Control by Juhana Leinonen.
Include Vorple Element Manipulation by Juhana Leinonen.
Include Vorple Hyperlinks by Juhana Leinonen.
Include Vorple Modal Windows by Juhana Leinonen.
Include Vorple Multimedia by Juhana Leinonen.
Include Vorple Notifications by Juhana Leinonen.
Include Vorple Screen Effects by Juhana Leinonen.
Include Vorple Status Line by Juhana Leinonen.
Include Vorple Tooltips by Juhana Leinonen.

There is a room.

When play begins:
	construct the Vorple status line with 3 columns.

[make manual testing easier by inserting dummy testing functions usually provided by the testing framework]
When play begins:
	execute JavaScript command "if(!window.passI7Test) window.passI7Test = function() {}; if(!window.failI7Test) window.failI7Test = function() {};"

Volume 1 - Extension tests

Book 1 - Vorple

Chapter 1 - Handshake

Carry out unit testing "handshake":
	whether or not Vorple is supported confirms "Vorple support";
	execute JavaScript command "return vorple.version";
	whether or not text returned by the JavaScript command exactly matches the regular expression "\d\.\d{1,2}(\.\d{1,2})?(\-<a-z>*)?" confirms "Version number ([text returned by the JavaScript command])";


Chapter 2 - User Interface

Vorple interface setup rule:
	set a shared flag "ui setup".

UI state is a list of truth states that varies. UI state is { true, false, false }.

Vorple interface update rule:
	repeat with N running from 1 to the number of entries in the UI state:
		if entry N in the UI state is true:
			set a shared flag "ui update [N]";
		otherwise:
			unset a shared flag "ui update [N]".

A Vorple interface update rule:
	if turn count is less than 2:
		say "Start of game"; [tests that printing something before the game starts doesn't crash the interpreter]

Every turn:
	now entry 2 in UI state is true.

Carry out unit testing "state 3 on":
	now entry 3 in UI state is true.


Chapter 3 - JavaScript evaluation

Carry out unit testing "string escaping":
	let unclean text be "\ Testy 'Tester' O'Testface /";
	place an element called "string-escaping";
	execute JavaScript command "$('.string-escaping').text('[escaped unclean text]')".

Carry out unit testing "string escaping with line breaks":
	let unclean text be "\ Testy[line break] 'Tester'[line break] O'Testface[line break] /";
	place an element called "string-escaping-no-linebreaks";
	execute JavaScript command "$('.string-escaping-no-linebreaks').text('[escaped unclean text]')".

Carry out unit testing "string escaping with line break changes":
	let unclean text be "\ [line break] Testy [line break] 'Tester' [line break] O'Testface [line break] /";
	let asterisks be "**";
	place an element called "string-escaping-linebreaks-change";
	execute JavaScript command "$('.string-escaping-linebreaks-change').text('[escaped unclean text using asterisks as line breaks]')".

[This test doubles as performance testing]
Carry out unit testing "JS evaluation":
	execute JavaScript command "console.time()";
  let return value be "";
	execute JavaScript command "return 'one two three'";
	now return value is the value returned by the JavaScript command;
	expect the text returned by the JavaScript command to be "one two three" which confirms "Text return value";
	expect the type of the value returned by the JavaScript command to be "text" which confirms "Text type";
	execute JavaScript command "return 'one \'two\' three'";
	expect the text returned by the JavaScript command to be "one 'two' three" which confirms "Text with quotes";
	execute JavaScript command "return ''";
	expect the text returned by the JavaScript command to be "" which confirms "Empty string";
	execute JavaScript command "return 'one\ntwo\nthree'";
	expect the text returned by the JavaScript command to be "one[line break]two[line break]three" which confirms "Text with line breaks";
	execute JavaScript command "return 1+2";
	now return value is the value returned by the JavaScript command;
	expect the number returned by the JavaScript command to be 3 which confirms "Number return value";
	expect the type of the value returned by the JavaScript command to be "number" which confirms "Number type";
	execute JavaScript command "return '4'";
	expect the number returned by the JavaScript command to be 4 which confirms "Number returned as text";
	execute JavaScript command "return '-4'";
	expect the number returned by the JavaScript command to be -4 which confirms "Negative number returned as text";
	execute JavaScript command "return 3.2";
	expect the number returned by the JavaScript command to be 3 which confirms "Positive reals rounded down";
	execute JavaScript command "return 3.6";
	expect the number returned by the JavaScript command to be 4 which confirms "Positive reals rounded up";
	execute JavaScript command "return 3.5";
	expect the number returned by the JavaScript command to be 4 which confirms "Positive real halfpoint rounded up";
	execute JavaScript command "return 3.501";
	expect the number returned by the JavaScript command to be 4 which confirms "Positive real near halfpoint rounded up";
	execute JavaScript command "return -3.5";
	expect the number returned by the JavaScript command to be -3 which confirms "Negative real halfpoint rounded towards zero";
	execute JavaScript command "return -3.51";
	expect the number returned by the JavaScript command to be -4 which confirms "Negative real halfpoint rounded up";
	execute JavaScript command "return -3.5000";
	expect the number returned by the JavaScript command to be -3 which confirms "Negative real halfpoint with trailing zeroes rounded towards zero";
	execute JavaScript command "return 2147483647";
	expect the number returned by the JavaScript command to be 2147483647 which confirms "Number at Glulx upper range";
	execute JavaScript command "return -2147483647";
	expect the number returned by the JavaScript command to be -2147483647 which confirms "Number at Glulx lower range";
	execute JavaScript command "return 2147483648";
	now run-time error thrown is false;
	expect the number returned by the JavaScript command to be 0 which confirms "Number exceeding Glulx upper range returns 0";
	whether or not run-time error thrown is true confirms "Number exceeding Glulx upper range throws an error";
	execute JavaScript command "return -2147483648";
	now run-time error thrown is false;
	expect the number returned by the JavaScript command to be 0 which confirms "Number exceeding Glulx lower range returns 0";
	whether or not run-time error thrown is true confirms "Number exceeding Glulx lower range throws an error";
	execute JavaScript command "return 1e30";
	expect the value returned by the JavaScript command to be "1000000000000000000000000000000" which confirms "Scientific notation is converted to plain integer";
	execute JavaScript command "return true";
	expect the type of the value returned by the JavaScript command to be "truth state" which confirms "Truth state type";
	execute JavaScript command "return true";
	whether or not the JavaScript command returned true confirms "Truth state value (true)";
	execute JavaScript command "return false";
	whether or not the JavaScript command returned false confirms "Truth state value (false)";
	execute JavaScript command "return 1";
	now run-time error thrown is false;
	expect whether or not the JavaScript command returned true to be false which confirms "Retrieving the truth state of non-boolean returns false";
	whether or not run-time error thrown is true confirms "Retrieving the truth state of non-boolean throws an error";
	execute JavaScript command "return [bracket]1,2,3[close bracket]";
	expect the type of the value returned by the JavaScript command to be "list" which confirms "List type";
	execute JavaScript command "return ({a:1, b:2})";
	expect the type of the value returned by the JavaScript command to be "object" which confirms "Object type";
	execute JavaScript command "return 1/0";
	now return value is the value returned by the JavaScript command;
	expect the return value to be "Infinity" which confirms "Infinity value";
	expect the type of the value returned by the JavaScript command to be "infinity" which confirms "Infinity type";
	execute JavaScript command "return -1/0";
	now return value is the value returned by the JavaScript command;
	expect the return value to be "-Infinity" which confirms "Negative infinity value";
	expect the type of the value returned by the JavaScript command to be "infinity" which confirms "Negative infinity type";
	execute JavaScript command "return (function(){})";
	expect the type of the value returned by the JavaScript command to be "function" which confirms "Function type";
	execute JavaScript command "return 'foo'";
	execute JavaScript command "'bar'";
	expect the text returned by the JavaScript command to be "foo" which confirms "Implicit return values ignored";
	execute JavaScript command "return undefined";
	expect the text returned by the JavaScript command to be "foo" which confirms "Return undefined is ignored";
	execute JavaScript command "return null";
	now return value is the value returned by the JavaScript command;
	expect the return value to be "null" which confirms "Null value";
	expect the type of the value returned by the JavaScript command to be "nothing" which confirms "Null type";
	execute JavaScript command "console.timeEnd()";

[Shortcut for manual testing]
Performance testing is an action out of world.
Understand "perf" as performance testing.
Carry out performance testing: try unit testing "js evaluation".

Carry out unit testing "circular references":
  [The following creates an object with a circular reference. Stringifying it will throw an error, which shouldn't crash the game or throw an error in the interpreter. Instead, null should be returned.]
  execute JavaScript command "var a = {}; a.a = a; return a";
	expect the value returned by the JavaScript command to be "null" which confirms "Circular reference evaluates to null".

Carry out unit testing "nested evaluation":
	execute JavaScript command "var a = '[escaped nested expression result]'; return a";
	expect the text returned by the JavaScript command to be "test" which confirms "Nested evaluation doesn't throw an error".

To decide which text is the nested expression result:
	execute JavaScript command "return 'test'";
	decide on the text returned by the JavaScript command.


Chapter 4 - DOM manipulation

Carry out unit testing "create containers":
	open HTML tag "div" called "testdiv";
	say "1";
	open HTML tag "span" called "testspan";
	say "2";
	close HTML tag; [span]
	say "3";
	close HTML tag; [div]

Container counter is a number that varies.

[TODO: break this up into smaller rules and have the test runner check results]
Carry out unit testing "element manipulation":
	open HTML tag "div" called "element-unittest-container";
	place a "span" element called "unittest[container counter]" reading "one two three";
	execute JavaScript command "return $('#output .element-unittest-container span.unittest[container counter]').length";
	expect the number returned by the JavaScript command to be 1 which confirms "Custom element created";
	execute JavaScript command "return $('#output .element-unittest-container span.unittest[container counter]').text()";
	expect the text returned by the JavaScript command to be "one two three" which confirms "Custom element has correct content";
	increment container counter;
	place a "span" element called "unittest[container counter]" reading "one[line break]two

	three";
	execute JavaScript command "return $('#output .element-unittest-container span.unittest[container counter]').text()";
	expect the text returned by the JavaScript command to be "one[line break]two[line break][line break]three" which confirms "Content with line breaks";
	increment container counter;
	place a "span" element called "unittest[container counter]" reading "<span>test</span>";
	execute JavaScript command "return $('#output .element-unittest-container span.unittest[container counter]').text()";
	expect the text returned by the JavaScript command to be "<span>test</span>" which confirms "HTML content is encoded";
	increment container counter;
	place a "section" element called "unittest[container counter]";
	execute JavaScript command "return $('#output .element-unittest-container section.unittest[container counter]').length";
	expect the number returned by the JavaScript command to be 1 which confirms "Empty custom element created";
	increment container counter;
	open HTML tag "div" called "unittest[container counter]";
	place a "p" element reading "four five six";
	place a "div" element;
	close HTML tag;
	execute JavaScript command "return $('#output .element-unittest-container div.unittest[container counter] > p').attr('class')";
	expect the text returned by the JavaScript command to be "" which confirms "Element without class created";
	execute JavaScript command "return $('#output .element-unittest-container div.unittest[container counter] > div').attr('class')";
	expect the text returned by the JavaScript command to be "" which confirms "Plain element created, class list empty";
	execute JavaScript command "return $('#output .element-unittest-container div.unittest[container counter] > div').text()";
	expect the text returned by the JavaScript command to be "" which confirms "Plain element created with no content";
	increment container counter;
	place an inline element called "unittest[container counter]" reading "seven eight nine";
	execute JavaScript command "return $('#output .element-unittest-container .unittest[container counter]').css('display')";
	expect the text returned by the JavaScript command to be "inline" which confirms "Inline element created, is inline";
	execute JavaScript command "return $('#output .element-unittest-container .unittest[container counter]').text()";
	expect the text returned by the JavaScript command to be "seven eight nine" which confirms "Inline element created with correct content";
	increment container counter;
	place an inline element called "unittest[container counter]";
	execute JavaScript command "return $('#output .element-unittest-container .unittest[container counter]').css('display')";
	expect the text returned by the JavaScript command to be "inline" which confirms "No-content inline element created, is inline";
	execute JavaScript command "return $('#output .element-unittest-container .unittest[container counter]').text()";
	expect the text returned by the JavaScript command to be "" which confirms "No-content inline element has no content";
	increment container counter;
	open HTML tag "div" called "unittest[container counter]";
	place an inline element reading "inline noclass";
	close HTML tag;
	execute JavaScript command "return $('#output .element-unittest-container div.unittest[container counter] > span').css('display')";
	expect the text returned by the JavaScript command to be "inline" which confirms "No-class inline element created, is inline";
	execute JavaScript command "return $('#output .element-unittest-container div.unittest[container counter] > span').attr('class')";
	expect the text returned by the JavaScript command to be "" which confirms "No-class inline element created, class list empty";
	execute JavaScript command "return $('#output .element-unittest-container div.unittest[container counter] > span').text()";
	expect the text returned by the JavaScript command to be "inline noclass" which confirms "No-class inline element created with correct content";
	increment container counter;
	place a block level element called "unittest[container counter]" reading "seven eight nine";
	execute JavaScript command "return $('#output .element-unittest-container .unittest[container counter]').css('display')";
	expect the text returned by the JavaScript command to be "block" which confirms "Inline element created";
	execute JavaScript command "return $('#output .element-unittest-container .unittest[container counter]').text()";
	expect the text returned by the JavaScript command to be "seven eight nine" which confirms "Inline element created with correct content";
	increment container counter;
	place a block level element called "unittest[container counter]";
	execute JavaScript command "return $('#output .element-unittest-container .unittest[container counter]').css('display')";
	expect the text returned by the JavaScript command to be "block" which confirms "No-content block element created";
	execute JavaScript command "return $('#output .element-unittest-container .unittest[container counter]').text()";
	expect the text returned by the JavaScript command to be "" which confirms "No-content block element has no content";
	increment container counter;
	open HTML tag "div" called "unittest[container counter]";
	place a block level element reading "block noclass";
	close HTML tag;
	execute JavaScript command "return $('#output .element-unittest-container div.unittest[container counter] > div').css('display')";
	expect the text returned by the JavaScript command to be "block" which confirms "No-class block element created, is block level";
	execute JavaScript command "return $('#output .element-unittest-container div.unittest[container counter] > div').attr('class')";
	expect the text returned by the JavaScript command to be "" which confirms "No-class block element created, class list empty";
	execute JavaScript command "return $('#output .element-unittest-container div.unittest[container counter] > div').text()";
	expect the text returned by the JavaScript command to be "block noclass" which confirms "No-class block element created with correct content";
	increment container counter;
	place a "span" element called "unittest[container counter]";
	place a "span" element called "unittest[container counter]";
	display text "last" in the element called "unittest[container counter]";
	execute JavaScript command "return $('#output .element-unittest-container span.unittest[container counter]:first').text()";
	expect the text returned by the JavaScript command to be "" which confirms "Display content in one of multiple elements, first element empty";
	execute JavaScript command "return $('#output .element-unittest-container span.unittest[container counter]:last').text()";
	expect the text returned by the JavaScript command to be "last" which confirms "Display content in one of multiple elements, content in last element";
	display text "all" in all the elements called "unittest[container counter]";
	execute JavaScript command "return $('#output .element-unittest-container span.unittest[container counter]:first').text()";
	expect the text returned by the JavaScript command to be "all" which confirms "Display content in all multiple elements, content in first element";
	execute JavaScript command "return $('#output .element-unittest-container span.unittest[container counter]:last').text()";
	expect the text returned by the JavaScript command to be "all" which confirms "Display content in all multiple elements, content in last element";
	close HTML tag; [element-unittest-container]
	set output focus to the element called "element-unittest-container";
	increment container counter;
	place an element called "unittest[container counter]" reading "parent container focus";
	increment container counter;
	place a block level element called "unittest[container counter]";
	set output focus to the element called "unittest[container counter]";
	say "focus";
	set output focus to the main window;
	execute JavaScript command "return $('#output .element-unittest-container .unittest[container counter]').text()";
	expect the text returned by the JavaScript command to be "focus" which confirms "Setting output focus";
	whether or not element called "unittest[container counter]" exists confirms "Existence of an existing element";
	whether or not element called "unittest[container counter + 1]" doesn't exist confirms "Existence of a non-existing element";
	place an element called "unittest[container counter]";
	expect the number of elements called "unittest[container counter]" to be 2 which confirms "Element count";


Chapter 5 - Unicode characters

Carry out unit testing "unicode":
	place an element called "unicode-test";
	let Unicode string be "ÜNÏCÖDÉ[unicode 8264]";
	execute JavaScript command "$('.unicode-test').text('[escaped Unicode string]')".


Chapter 6 - Prompt

Carry out unit testing "prompt in confirmation":
	place an element called "yes-no-test";
	set output focus to the element called "yes-no-test";
	if the player consents:
		do nothing;
	set output focus to the main window.

Carry out unit testing "prompt at game over":
	end the story.


Book 2 - Command Prompt Control

Chapter 1 - Command queue

Carry out unit testing "command queue":
	queue parser command "unittest add to queue";
	expect test result "Queueing parser commands";
	queue parser command "unittest null";
	queue parser command "unittest silent command queue", without showing the command;
	expect test result "Queueing silent parser commands";
	queue parser command "unittest command queue order 1";
	queue parser command "unittest command queue order 2";
	expect test result "Parser command queue order".

Carry out unit testing "add to queue":
	resolve test result "Queueing parser commands".

Carry out unit testing "silent command queue":
	execute JavaScript command "return $('.lineinput.last .prompt-input').text()";
	if the text returned by the JavaScript command is "unittest null":
		resolve test result "Queueing silent parser commands".

Correct command queue order is a truth state that varies. Correct command queue order is false.

Carry out unit testing "command queue order 1":
	now correct command queue order is true.

Carry out unit testing "command queue order 2":
	if correct command queue order is true:
		resolve test result "Parser command queue order".


Chapter 2 - Command history

[TODO: break this up into smaller rules and have the test runner check results]
Carry out unit testing "command history":
	add the command "test" to the command history;
	execute JavaScript command "return haven.prompt.history.get().pop()";
	expect text returned by the JavaScript command to be "test" which confirms "Adding history";
	add the command "test 2" to the command history;
	remove the last command from the command history;
	execute JavaScript command "return haven.prompt.history.get().pop()";
	expect the text returned by the JavaScript command to be "test" which confirms "Removing history";
	execute JavaScript command "return haven.prompt.history.get().length";
	let history size be the number returned by the JavaScript command;
	change the last command in the command history to "changed";
	execute JavaScript command "return haven.prompt.history.get().pop()";
	expect the text returned by the JavaScript command to be "changed" which confirms "Changing history";
	execute JavaScript command "return haven.prompt.history.get().length";
	expect the number returned by the JavaScript command to be history size which confirms "Changing history doesn't change history size";
	clear the command history;
	execute JavaScript command "return haven.prompt.history.get().length";
	expect the number returned by the JavaScript command to be 0 which confirms "Clearing the entire history".


Chapter 3 - Command line manipulation

Carry out unit testing "command line manipulation":
	prefill the command line with "test".

Carry out unit testing "queue when prompt available":
	resolve test result "Command queue when prompt is available";
	queue parser command "unittest finish".


Book 3 - Element Manipulation

Carry out unit testing "move elements":
	place an inline element called "aftercontainer";
	set output focus to the element called "aftercontainer";
	place an inline element called "after2" reading "a2";
	place an inline element called "after1" reading "a1";
	move the element called "after2" after "after1";
	set output focus to the main window;
	place an inline element called "beforecontainer";
	set output focus to the element called "beforecontainer";
	place an inline element called "before2" reading "b2";
	place an inline element called "before1" reading "b1";
	move the element called "before1" before "before2";
	set output focus to the main window.


Book 4 - Hyperlinks

Chapter 1 - URL links

Carry out unit testing "URL hyperlinks":
	open HTML tag "div" called "url-hyperlinks";
	place a link to the web site "http://vorple-if.com" called "hyperlink1" reading "link1";
	place a link to the web site "http://vorple-if.com" called "hyperlink2" reading "link2", opening in the same window;
	place a link to the web site "http://vorple-if.com" called "hyperlink3";
	place a link to the web site "http://vorple-if.com" reading "link4";
	place a link to the web site "http://vorple-if.com";
	place a link to the web site "http://example.com";
	close HTML tag.


Chapter 2 - Command links

Carry out unit testing "command hyperlinks":
	increment container counter;
	open HTML tag "div" called "command-hyperlinks";
	place a link to the command "unittest command link 1" called "commandlink1" reading "link1";
	place a link to the command "unittest command link 2" called "commandlink2" reading "silent", without showing the command;
	place a link to the command "unittest command link 3" called "commandlink3";
	place a link to the command "unittest command link 4" reading "link4";
	place a link to the command "unittest command link 5";
	close HTML tag;

Carry out unit testing "command link 1":
	set a shared flag "command link 1".

Carry out unit testing "command link 2":
	set a shared flag "command link 2".

Carry out unit testing "command link 3":
	set a shared flag "command link 3".


Chapter 3 - JavaScript links

Carry out unit testing "JS hyperlinks":
	open HTML tag "div" called "js-hyperlinks";
	place a link to execute the JavaScript command "vorple.prompt.queueCommand('unittest js link 1')" called "jslink1" reading "link1";
	place a link to execute the JavaScript command "vorple.prompt.queueCommand('unittest js link 2')" reading "link2";
	close HTML tag;

Carry out unit testing "js link 1":
	set a shared flag "js link 1".


Chapter 4 - Disabling links

Carry out unit testing "disabling one link":
	disable the link called "hyperlink1".

Carry out unit testing "disabling links in containers":
	disable all links inside element called "hyperlink-unittest".

Carry out unit testing "disabling all links":
	disable all links.


Book 5 - Modal Windows

Carry out unit testing "show modal":
	show a modal window reading "Test".

Carry out unit testing "create empty modal":
	show a modal window;
	set output focus to the modal window;
	say "Test2";
	set output focus to the main window.

Carry out unit testing "prevent keypress input in modal":
	open HTML tag "div" called "output-while-modal";
	say "1";
	show a modal window;
	wait for any key;
	say "2";
	close HTML tag.

Carry out unit testing "modal cleanup":
	remove the element called "output-while-modal".


Book 6 - Multimedia

Carry out unit testing "image":
	place the image "/media/vorple.png" called "logo" with description "Vorple's logo", centered.

Carry out unit testing "music":
	play music file "media/winding.mp3".

Carry out unit testing "music force restart":
	play music file "media/winding.mp3", always from the start.

Pl is a list of text that varies. Pl is {"media/1.wav", "media/2.wav", "media/3.wav", "media/4.wav", "media/5.wav"}.

Carry out unit testing "pl":
	start the playlist pl.


Book 7 - Notifications

Carry out unit testing "default notification":
	display a notification reading "Test notification".


Book 8 - Screen Effects

Carry out unit testing "style line breaks":
	place an element called "style-lb-test";
	set output focus to the element called "style-lb-test";
	say "[strong font style]foo.[end style] bar";
	set output focus to the main window.

Carry out unit testing "clearing the screen":
	say "Before clearing the screen.";
	clear the screen;
	say "After clearing the screen."

Carry out unit testing "clearing the main screen":
	say "Before clearing the main screen.";
	clear only the main screen;
	say "After clearing the main screen."

Carry out unit testing "waiting for space":
    say "Before waiting for space.";
    wait for the SPACE key;
    say "After waiting for space."

Carry out unit testing "adding page styles":
	apply cyan background style to the page;
	apply fantasy font style to the page.

Carry out unit testing "removing page styles":
	remove the fantasy font style from the page.

Carry out unit testing "cascading page styles":
	apply red background style to the page;
	apply cyan background style to the page.


Book 9 - Tooltips

Carry out unit testing "text with tooltip on hover":
	place text "tooltip test" called "tooltip-test" with a tooltip reading "tooltip contents".

Carry out unit testing "tooltip on demand":
	place an inline element called "tooltip-on-demand" reading "tooltip test";
	display a tooltip "tooltip on demand test" on the element called "tooltip-on-demand" after 1 second for 2 seconds.

Carry out unit testing "tooltip on prompt":
	display a tooltip "tooltip on prompt test" on the prompt indefinitely.

Carry out unit testing "hiding tooltips":
	hide the tooltip.


Volume 2 - Testing framework

Book 1 - Unit testing command

Unit testing is an action applying to one topic.
Understand "unittest [text]" as unit testing.

Fail count is a number that varies. Fail count is 0.
Pass count is a number that varies. Pass count is 0.
Run-time error thrown is a truth state that varies. Run-time error thrown is false.
Current test name is text that varies.

[this overrides the error printing in the Vorple extension]
To throw Vorple run-time error (desc – text):
	now run-time error thrown is true.

To start tests for (name - text):
	say "[bold type][name][roman type][line break]";

To name the test (name - text):
	say "  [name]: ";
  now current test name is name.

To pass the test:
	place an inline element called "textcolor-2" reading "PASSED[line break]";
	execute JavaScript command "passI7Test()";
	increment pass count.

To fail the test:
	place an inline element called "textcolor-4" reading "FAILED[line break]";
	execute JavaScript command "failI7Test('[escaped current test name]')";
	increment fail count.

To fail the test expecting (x - text) to be (y - text):
	place an inline element called "textcolor-4" reading "FAILED";
	if x is "":
		now x is "(empty)";
	if y is "":
		now y is "(empty)";
	say " - expected [x] to be [y][line break]";
	execute JavaScript command "failI7Test('[escaped current test name]')";
	increment fail count.

To (x - truth state) confirms (msg - text):
	name the test msg;
	if x is true:
		pass the test;
	otherwise:
		fail the test.

To expect (x - text) to be (y - text) which confirms (msg - text):
 	name the test msg;
 	if x is y:
 		pass the test;
 	otherwise:
 		fail the test expecting x to be y.

To expect (x - number) to be (y - number) which confirms (msg - text):
	expect "[x]" to be "[y]" which confirms msg.

To expect (x - truth state) to be (y - truth state) which confirms (msg - text):
	expect "[x]" to be "[y]" which confirms msg.

Test result expectations is a list of text that varies.

To expect test result (msg - text):
	add msg to test result expectations.

To resolve test result (msg - text):
	remove msg from test result expectations;
	say "  [msg]: ";
	pass the test.


Book 2 - Flags

Setting a flag is an action out of world applying to a topic.

Understand "set flag [text]" as setting a flag.

Carry out setting a flag:
	set a shared flag topic understood.

To set a shared flag (flag name - text):
	execute JavaScript command "window.testFlags[bracket]'[escaped flag name]'[close bracket] = true".

To unset a shared flag (flag name - text):
	execute JavaScript command "window.testFlags[bracket]'[escaped flag name]'[close bracket] = false".

First Vorple interface setup rule (this is the create the flag object rule):
	execute JavaScript command "window.testFlags = {}".


Book 3 - Helpers

Generating content is an action out of world.

Understand "generate content" as generating content.

Carry out generating content:
	repeat with N running from 1 to 20:
		say "Lorem ipsum dolor sit amet, consectetur adipiscing elit, sed do eiusmod tempor incididunt ut labore et dolore magna aliqua. Ut enim ad minim veniam, quis nostrud exercitation ullamco laboris nisi ut aliquip ex ea commodo consequat. Duis aute irure dolor in reprehenderit in voluptate velit esse cillum dolore eu fugiat nulla pariatur. Excepteur sint occaecat cupidatat non proident, sunt in culpa qui officia deserunt mollit anim id est laborum."
